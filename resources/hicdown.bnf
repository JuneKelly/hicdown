(* A document is a sequence of blocks, separated by blank lines *)
Document = <#'\n*'> Block (<dnl> Block)* <#'\n*'>

(* Spaces *)
<spc> = <#'\s'>

(* Mandatory spaces *)
<mspc> = <spc+>

(* Optional spaces *)
<ospc> = <spc*>

(* "Double" newlines (any sequence of blank lines) *)
dnl = #'\n(\n)+'

(* Single newlines *)
nl = !dnl <#'\n'>

(* A block consists of plain text, and/or segments *)
Block = Content

<Content> = Segment Content
    | (&'%%%' VerbatimText | !'%%%' Text) (Segment Content | Epsilon)
    | Epsilon

Text = (textchar | esc)+

<esc> = '\\\\' | '\\[' | '\\]' | '\\{' | '\\}' | '\\\n'

<textchar> = !'\n\n' #'[^\[\]{}]'

(* A segment is enclosed in square brackets, with a tag and optional attr map,
   and contains a sequence of plain text and/or other segments *)
Segment = <'['> (
  (tag)
  | (tag mspc Content)
  | (tag mspc Attrs)
  | (tag mspc Attrs mspc Content)
) <']'>

tag = #':[^\s]*'

(* An attribute map is enclosed in curly braces, and consists of key-value pairs *)
Attrs = <'{'> ospc <'}'>
    | <'{'> ospc Pair ospc <'}'>
    | <'{'> ospc Pair (mspc Pair)* ospc <'}'>

Pair = key <spc+> (val | Qval)

key = #':[\w]+'
val = #'[^\s}"\']+'

Qval = <'"'> (valchar | valesc)* <'"'>
<valchar> = #'[^"]'
valesc = '\\"'


(* Verbatim text *)
VerbatimText = '%%%' NestedVerbatim '%%%'
<NestedVerbatim> = VerbatimChar+ | ('%' NestedVerbatim '%')
<VerbatimChar> = '\n' | #'.'
